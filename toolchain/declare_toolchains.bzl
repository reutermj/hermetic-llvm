load("//platforms:common.bzl", "MSVC_TARGET_STAGE0_SUPPORTED_EXECS", "SUPPORTED_EXECS", "SUPPORTED_TARGETS")
load("//runtimes:cc_runtimes.bzl", "CC_RUNTIMES_TOOLCHAIN_TYPE", "bazel_supports_cc_runtimes_toolchain")
load("//toolchain:selects.bzl", "clang_cl_resource_dir_arg", "platform_cc_tool_map", "platform_module_map", "resource_dir_arg")
load(":cc_toolchain.bzl", "cc_toolchain")

def declare_toolchains(*, execs = SUPPORTED_EXECS, targets = SUPPORTED_TARGETS):
    """Declares the configured LLVM toolchains.

    Args:
        execs: List of (os, arch) tuples describing exec platforms.
        targets: List of (os, arch) tuples describing target platforms.
    """
    for (exec_os, exec_cpu) in execs:
        cc_toolchain_name = exec_os + "_" + exec_cpu + "_cc_toolchain"

        # Even though `tool_map` has an exec transition, Bazel doesn't properly handle
        # binding a single `cc_toolchain` to multiple toolchains with different `exec_compatible_with`.
        # See https://github.com/bazelbuild/rules_cc/issues/299#issuecomment-2660340534
        cc_toolchain(
            name = cc_toolchain_name,
            tool_map = platform_cc_tool_map(exec_os, exec_cpu),
            module_map = platform_module_map(exec_os, exec_cpu),
            # Paths below describe the concrete execution filesystem. Keep
            # target semantics in //toolchain's ordered argument composition.
            extra_args = select({
                "@llvm//platforms/config:windows_x86_64_msvc": [
                    "@llvm//toolchain/args/windows/msvc:normalized_default_libs_for_runtime",
                    clang_cl_resource_dir_arg(exec_os, exec_cpu),
                    "@llvm//toolchain/args/windows/msvc:normalized_sdk_compile_args",
                ],
                "@llvm//platforms/config:windows_aarch64_msvc": [
                    "@llvm//toolchain/args/windows/msvc:normalized_default_libs_for_runtime",
                    clang_cl_resource_dir_arg(exec_os, exec_cpu),
                    "@llvm//toolchain/args/windows/msvc:normalized_sdk_compile_args",
                ],
                "//conditions:default": [resource_dir_arg(exec_os, exec_cpu)],
            }),
        )

        for (target_os, target_cpu) in targets:
            target_settings = ["@llvm//toolchain:bootstrap_stage0_prebuilt_seed"]
            if target_os == "windows":
                target_settings.append("@llvm//platforms/config:windows_" + target_cpu + "_mingw_compatible")

            native.toolchain(
                name = exec_os + "_" + exec_cpu + "_to_" + target_os + "_" + target_cpu,
                exec_compatible_with = [
                    "@platforms//cpu:" + exec_cpu,
                    "@platforms//os:" + exec_os,
                ],
                target_compatible_with = [
                    "@platforms//cpu:" + target_cpu,
                    "@platforms//os:" + target_os,
                ],
                target_settings = target_settings,
                toolchain = cc_toolchain_name,
                toolchain_type = "@bazel_tools//tools/cpp:toolchain_type",
                visibility = ["//visibility:public"],
            )

            if target_os == "windows" and (exec_os, exec_cpu) in MSVC_TARGET_STAGE0_SUPPORTED_EXECS:
                native.toolchain(
                    name = exec_os + "_" + exec_cpu + "_to_" + target_os + "_" + target_cpu + "_msvc",
                    exec_compatible_with = [
                        "@platforms//cpu:" + exec_cpu,
                        "@platforms//os:" + exec_os,
                    ],
                    target_compatible_with = [
                        "@platforms//cpu:" + target_cpu,
                        "@platforms//os:" + target_os,
                    ],
                    target_settings = [
                        "@llvm//toolchain:bootstrap_stage0_prebuilt_seed",
                        "@llvm//platforms/config:windows_" + target_cpu + "_msvc",
                    ],
                    toolchain = cc_toolchain_name,
                    toolchain_type = "@bazel_tools//tools/cpp:toolchain_type",
                    visibility = ["//visibility:public"],
                )

    if not bazel_supports_cc_runtimes_toolchain():
        return

    # C++ runtimes toolchain: rules_cc links the runtimes it lists into every
    # C++ target built for the platform. It is resolved for user programs only
    # (runtime_stage = complete); the runtimes themselves are built at earlier
    # stages, which keeps them from depending on themselves. Linux only for now:
    # macOS takes libc++ from the SDK and Windows keeps the cc_toolchain
    # static_runtime_lib/dynamic_runtime_lib path. See //runtimes:cc_runtimes.bzl.
    for (target_os, target_cpu) in targets:
        if target_os != "linux":
            continue

        native.toolchain(
            name = "cc_runtimes_" + target_os + "_" + target_cpu,
            target_compatible_with = [
                "@platforms//cpu:" + target_cpu,
                "@platforms//os:" + target_os,
            ],
            target_settings = ["@llvm//toolchain:runtimes_all"],
            toolchain = "@llvm//runtimes/cxxstdlib:cc_runtimes",
            toolchain_type = CC_RUNTIMES_TOOLCHAIN_TYPE,
            visibility = ["//visibility:public"],
        )
