load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:common_settings.bzl", "BuildSettingInfo", "bool_flag", "string_flag")

OPTIMIZATION_MODES = [
    "debug",
    "optimized",
]

SANITIZERS = [
    "ubsan",
    "cfi",
    "msan",
    "dfsan",
    "nsan",
    "safestack",
    "rtsan",
    "tysan",
    "tsan",
    "asan",
    "lsan",
    "xray",
    "fuzzer",
    "profile",
]

def _is_exec_configuration(ctx):
    # TODO(cerisier): Is there a better way to detect cfg=exec?
    return ctx.genfiles_dir.path.find("-exec") != -1

def _target_bool_flag_impl(ctx):
    value = str(ctx.attr.setting[BuildSettingInfo].value).lower()
    if _is_exec_configuration(ctx):
        value = "false"
    return [config_common.FeatureFlagInfo(value = value)]

_target_bool_flag = rule(
    implementation = _target_bool_flag_impl,
    attrs = {
        "setting": attr.label(mandatory = True),
    },
)

def _host_bool_flag_impl(ctx):
    value = str(ctx.attr.setting[BuildSettingInfo].value).lower()
    if not _is_exec_configuration(ctx):
        value = "false"
    return [config_common.FeatureFlagInfo(value = value)]

_host_bool_flag = rule(
    implementation = _host_bool_flag_impl,
    attrs = {
        "setting": attr.label(mandatory = True),
    },
)

def _declare_sanitizer_config_setting(sanitizer):
    target_setting_name = "target_" + sanitizer
    target_feature_name = sanitizer + "_target_config"
    target_config_setting = target_setting_name + "_enabled"
    _target_bool_flag(
        name = target_feature_name,
        # not target_setting_name
        setting = sanitizer,
    )
    native.config_setting(
        name = target_config_setting,
        flag_values = {
            target_feature_name: "true",
        },
    )

    host_setting_name = "host_" + sanitizer
    host_feature_name = sanitizer + "_host_config"
    host_config_setting = host_setting_name + "_enabled"
    _host_bool_flag(
        name = host_feature_name,
        setting = host_setting_name,
    )
    native.config_setting(
        name = host_config_setting,
        flag_values = {
            host_feature_name: "true",
        },
    )

    selects.config_setting_group(
        name = sanitizer + "_enabled",
        match_any = [
            target_config_setting,
            host_config_setting,
        ],
    )

def config_settings():
    # This flag controls the optimization mode for the compilation of the target
    # prequisites like the standard C library, the C++ standard library,
    # the unwinder, etc.
    #
    # Setting this to "debug" will compile these libraries with debug symbols,
    # frame pointers where applicable, and no optimizations.
    string_flag(
        name = "runtimes_optimization_mode",
        values = OPTIMIZATION_MODES,
        build_setting_default = "optimized",
    )

    for optimization_mode in OPTIMIZATION_MODES:
        native.config_setting(
            name = "runtimes_optimization_mode_{}".format(optimization_mode),
            flag_values = {
                ":runtimes_optimization_mode": optimization_mode,
            },
        )

    # This flag controls whether we compile and link with --sysroot=/dev/null
    # to ensure hermeticity.
    #
    # This is useful if dependencies that you do not control link against host system
    # libraries and you want to allow this behavior. (Hello rust_std).
    bool_flag(
        name = "empty_sysroot",
        build_setting_default = True,
    )

    # This flag makes dummy gcc, gcc_eh and gcc_s libraries available to link
    # against.
    #
    # This toolchain provides compiler-rt.builtins and libunwind through its
    # runtime libraries and never links against libgcc itself (see
    # experimental_use_llvm_libgcc below for a libgcc_s compatible unwinder).
    # Yet, it is possible for dependencies that you do not control to pass
    # -lgcc, -lgcc_eh or -lgcc_s linker flags.
    #
    # Since rustc passes -lgcc_s, we default to enabling this flag to make this
    # toolchain more broadly compatible out-of-the-box. If you know what you are
    # doing and do not want to no-op these flags, you can disable this behavior.
    # It is always on when experimental_use_llvm_libgcc is enabled.
    bool_flag(
        name = "experimental_stub_libgcc",
        build_setting_default = True,
    )

    # Compat: the former name of experimental_stub_libgcc.
    native.alias(
        name = "experimental_stub_libgcc_s",
        actual = ":experimental_stub_libgcc",
    )

    native.config_setting(
        name = "experimental_stub_libgcc_enabled",
        flag_values = {
            ":experimental_stub_libgcc": "True",
        },
    )

    # This flag replaces libunwind.so.1 with libgcc_s.so.1 as the dynamic
    # unwinder of Linux glibc targets, for artifacts that run on a distribution
    # using that system's libgcc.
    #
    # libgcc_s.so.1 is libunwind and the compiler-rt builtins linked into one
    # shared library whose exported symbols carry the GCC_* symbol versions of
    # GCC's libgcc_s.so.1, generated from the libgcc version scripts of the
    # GCC release that //constraints/cxxstdlib selects. Binaries then record
    # NEEDED libgcc_s.so.1 and import the versioned symbols the target
    # system's libgcc defines. See 3rd_party/llvm-project/x.x/llvm-libgcc/README.md.
    #
    # Only the dynamic runtime library changes: static links keep the static
    # libunwind, and -lgcc_s keeps resolving to the stub above.
    bool_flag(
        name = "experimental_use_llvm_libgcc",
        build_setting_default = False,
    )

    native.config_setting(
        name = "experimental_use_llvm_libgcc_enabled",
        flag_values = {
            ":experimental_use_llvm_libgcc": "True",
        },
    )

    selects.config_setting_group(
        name = "stub_libgcc_enabled",
        match_any = [
            ":experimental_use_llvm_libgcc_enabled",
            ":experimental_stub_libgcc_enabled",
        ],
    )

    # Whether the compiler-rt builtins are compiled with hidden visibility.
    # They normally are, so that every binary and shared object carries its
    # own private copy of the routines it uses. libgcc_s.so.1 must export them
    # instead, so its builder sets this to False for the builtins it links.
    bool_flag(
        name = "compiler_rt_builtins_hide_symbols",
        build_setting_default = True,
    )

    native.config_setting(
        name = "compiler_rt_builtins_hide_symbols_disabled",
        flag_values = {
            ":compiler_rt_builtins_hide_symbols": "False",
        },
    )

    for sanitizer in SANITIZERS:
        bool_flag(
            name = sanitizer,
            build_setting_default = False,
        )
        bool_flag(
            name = "host_{}".format(sanitizer),
            build_setting_default = False,
        )
        _declare_sanitizer_config_setting(sanitizer)
